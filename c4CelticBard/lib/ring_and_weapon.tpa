DEFINE_ACTION_FUNCTION ring_and_weapon BEGIN

	OUTER_FOR (i=1;i<6;++i) BEGIN
		// 按等级添加戒指和技能
		COPY ~%mod_root%/spl/base/c4cbrin0.spl~ ~override/c4cbrin%i%.spl~
			LPF ALTER_EFFECT STR_VAR resource=~c4cbrin%i%~ END
			FOR (j=1;j<6;++j) BEGIN
				LPF CLONE_EFFECT INT_VAR match_opcode=143 opcode=172 parameter1=0 STR_VAR resource=~c4cbcsw%j%~ END
			END
			LPF CLONE_EFFECT INT_VAR match_opcode=143 opcode=171 parameter1=0 STR_VAR resource=~c4cbcsw%i%~ END

		// 戒指
		COPY ~%mod_root%/itm/base/c4cbrin0.itm~ ~override/c4cbrin%i%.itm~
			PATCH_FOR_EACH match_opcode IN 0 325 BEGIN
				LPF ALTER_EFFECT INT_VAR match_opcode parameter1=i END
			END
			name=4000+i*100+1
			desc=4000+i*100+2
			WRITE_LONG 0x8 RESOLVE_STR_REF ((AT "%name%"))
			WRITE_LONG 0xc RESOLVE_STR_REF ((AT "%name%"))
			WRITE_LONG 0x50 RESOLVE_STR_REF ((AT "%desc%"))
			WRITE_LONG 0x54 RESOLVE_STR_REF ((AT "%desc%"))
			
		// 武器选择列表
		COPY ~%mod_root%/spl/base/c4cbcsw0.spl~ ~override/c4cbcsw%i%.spl~
			WRITE_ASCII 0x3a c4cbcswb
			LPF ALTER_SPELL_HEADER STR_VAR icon=c4cbcswb END
			LPF ALTER_EFFECT STR_VAR resource=~%DEST_RES%~ END
			desc=4010+i
			WRITE_LONG 0x8 RESOLVE_STR_REF ((AT 4010))
			WRITE_LONG 0x50 RESOLVE_STR_REF ((AT "%desc%"))
			
		COPY ~%mod_root%/2da/base/c4cbcsw0.2da~ ~override/c4cbcsw%i%.2da~
			SET_2DA_ENTRY_LATER weapon 0 1 ~C4CBSW%i%A~
			SET_2DA_ENTRY_LATER weapon 1 1 ~C4CBSW%i%B~
			SET_2DA_ENTRY_LATER weapon 2 1 ~C4CBSW%i%C~
			SET_2DA_ENTRIES_NOW weapon 3
			
		// 武器	
		ACTION_FOR_EACH type IN a b c BEGIN
		
			OUTER_SET tra_type = (~%type%~ STR_EQ a) ? 1 : (~%type%~ STR_EQ b) ? 2 : 3
			
			COPY ~%mod_root%/spl/base/c4cbsw0%type%.spl~ ~override/c4cbsw%i%%type%.spl~
				LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~%DEST_RES%~ END
				name=4000+i*100+tra_type*2+1
				WRITE_LONG 0x8 RESOLVE_STR_REF ((AT "%name%"))
				
			OUTER_SET prof = (~%type%~ STR_EQ a) ? 90 : (~%type%~ STR_EQ b) ? 91 : 97
			LAF CREATE_EFFECT INT_VAR opcode=233 target=1 duration=1 parameter1=i parameter2=prof STR_VAR name=~c4cbsw%i%%type%~ END
				
			COPY ~%mod_root%/itm/base/c4cbsw0%type%.itm~ ~override/c4cbsw%i%%type%.itm~
				WRITE_LONG 0x60 i
				LPF ALTER_ITEM_HEADER INT_VAR damage_bonus=i thac0_bonus=i END
				LPF ALTER_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~c4cbsw%i%%type%~ END
				LPF ALTER_EFFECT INT_VAR match_opcode=272 STR_VAR resource=~c4cbsw%i%%type%~ END
				PATCH_DEFINE_ASSOCIATIVE_ARRAY atb BEGIN
					1 => 0
					2 => 6
					3 => 1
					4 => 7
					5 => 2
				END
				PHP_EACH atb AS enchant => parameter1 BEGIN
					PATCH_IF enchant=i BEGIN
						LPF ALTER_EFFECT INT_VAR match_opcode=1 parameter1 END
					END
				END
				FOR (j=1;j<6;++j) BEGIN
					PATCH_FOR_EACH type1 IN a b c BEGIN
						LPF CLONE_EFFECT INT_VAR match_opcode=1 opcode=180 parameter1= RESOLVE_STR_REF (@4001) STR_VAR resource=~c4cbsw%j%%type1%~ END
					END
				END
				name=4000+i*100+tra_type*2+1
				desc=4000+i*100+tra_type*2+2
				WRITE_LONG 0x8 RESOLVE_STR_REF ((AT "%name%"))
				WRITE_LONG 0xc RESOLVE_STR_REF ((AT "%name%"))
				WRITE_LONG 0x50 RESOLVE_STR_REF ((AT "%desc%"))
				WRITE_LONG 0x54 RESOLVE_STR_REF ((AT "%desc%"))
				
				LPF CLONE_EFFECT INT_VAR match_opcode=1 opcode=146 parameter1=0 parameter2=1 STR_VAR resource=~c4cb!kit~ END

			// 武器其他职业不可用
			LAF CREATE_EMPTY_SPELL STR_VAR name=~c4cb!kit~ END
			COPY_EXISTING ~c4cb!kit.spl~ override
				LPF ADD_SPELL_EFFECT INT_VAR opcode=139 target=2 timing=1 parameter1= RESOLVE_STR_REF (@4000) END
				LPF ADD_SPELL_EFFECT INT_VAR opcode=12 target=2 timing=1 parameter1=9999 parameter2=268435456 END
				LPF ADD_SPELL_EFFECT INT_VAR opcode=112 target=2 timing=1 parameter1=0 parameter2=0 STR_VAR resource=~c4cbsw%i%%type%~ END

		END
	END

END
